// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.buildConfig = [
        'compileSdk': 28,
        'minSdk': 21,
        'targetSdk': 28,

        'version': [
            'major': 1,
            'minor': 0,
            'patch': 0,
            'build': 0,
        ],
    ]
    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
        'kotlin': '1.3.0',
        'kotlinCoroutines': '1.0.0',
        'kotlinSerialization': '0.9.0',
        'ktor': '1.0.0-beta-3',
        'sqldelight': '1.0.0-k1.3.0-a2',
        'knarchDb': '0.7.2-k1.3.0-a1',
        'timber': '5.0.0-k1.3.0-a1',
        'koin': '1.0.2',
        'androidx': [
            'lifecycle': '2.0.0'
        ],
        'groupie': '2.3.0',
        'glide': '4.8.0',
        'ktlint': '0.29.0',
    ]

    ext.deps = [
        'kotlin': [
            'stdlib': [
                'common': "org.jetbrains.kotlin:kotlin-stdlib-common:${versions.kotlin}",
                'jdk': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
            ],
            'coroutines': [
                'common': "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:${versions.kotlinCoroutines}",
                'jdk': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}",
                'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoroutines}",
                'native': "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:${versions.kotlinCoroutines}",
            ],
            'serialization': [
                'common': "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:${versions.kotlinSerialization}",
                'jdk': "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${versions.kotlinSerialization}",
                'native': "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:${versions.kotlinSerialization}",
            ],
        ],
        'ktor': [
            'client': [
                'core': [
                    'common': "io.ktor:ktor-client-core:${versions.ktor}",
                    'ios': "io.ktor:ktor-client-core-ios:${versions.ktor}",
                ],
                'okhttp': "io.ktor:ktor-client-okhttp:${versions.ktor}",
                'ios': "io.ktor:ktor-client-ios:${versions.ktor}",
                'json': [
                    'core': "io.ktor:ktor-client-json:${versions.ktor}",
                    'jvm': "io.ktor:ktor-client-json-jvm:${versions.ktor}",
                    'ios': "io.ktor:ktor-client-json-ios:${versions.ktor}",
                ],
            ],
        ],
        'sqldelight': [
            'runtime': "com.squareup.sqldelight:runtime-common:${versions.sqldelight}",
            'runtimeJdk': "com.squareup.sqldelight:runtime-jdk:${versions.sqldelight}",
            'runtimeNative': "com.squareup.sqldelight:sqldelightruntimeios:${versions.sqldelight}",
            'multiplatformdriver': "com.squareup.sqldelight:sqldelight-multiplatform-driver:${versions.sqldelight}",
            'multiplatformdriverNative': "com.squareup.sqldelight:sqldelightmultiplatformdriverios:${versions.sqldelight}",
            'multiplatformdriverAndroid': "com.squareup.sqldelight:sqldelight-multiplatform-android:${versions.sqldelight}",
        ],
        'knarch': [
            'db': "co.touchlab.knarch:knarch-common:${versions.knarchDb}",
            'dbAndroid': "co.touchlab.knarch:knarch-android:${versions.knarchDb}",
            'dbNative': "co.touchlab.knarch:knarch:${versions.knarchDb}",
        ],
        'timber' : [
            'common' : "com.jakewharton.timber:timber-common:${versions.timber}",
            'ios'    : "com.jakewharton.timber:timberNative:${versions.timber}",
            'android': "com.jakewharton.timber:timber-android:${versions.timber}"
        ],
        'androidx': [
            'appCompat': 'androidx.appcompat:appcompat:1.0.0',
            'recyclerView': 'androidx.recyclerview:recyclerview:1.0.0',
            'ktx': 'androidx.core:core-ktx:1.0.0',
            'constraintLayout': 'androidx.constraintlayout:constraintlayout:1.1.3',
            'lifecycleExtension': "androidx.lifecycle:lifecycle-extensions:${versions.androidx.lifecycle}",
            'browser': "androidx.browser:browser:1.0.0",
        ],
        'koin': [
            'core': "org.koin:koin-core:${versions.koin}",
            'android': "org.koin:koin-android:${versions.koin}",
            'viewModel': "org.koin:koin-androidx-viewmodel:${versions.koin}",
        ],
        'okhttp': [
            'logging': "com.squareup.okhttp3:logging-interceptor:3.11.0"
        ],
        'groupie': [
            'core': "com.xwray:groupie:${versions.groupie}",
            'dataBinding': "com.xwray:groupie-databinding:${versions.groupie}"
        ],
        'glide': [
            'core': "com.github.bumptech.glide:glide:${versions.glide}",
            'compiler': "com.github.bumptech.glide:compiler:${versions.glide}",
        ],
    ]

    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url "http://kotlin.bintray.com/kotlin-eap" }
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
        maven { url 'https://dl.bintray.com/touchlabpublic/kotlin' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.16.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlinx" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url 'https://dl.bintray.com/touchlabpublic/kotlin' }
        maven { url "https://dl.bintray.com/kotlin/ktor" }
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
        }
    }

    afterEvaluate {
        def preBuildTask = tasks.findByName("preBuild")
        if (preBuildTask != null) {
            preBuild.dependsOn spotlessKotlinApply
            return
        }

        def compileKotlinTask = tasks.findByName("compileKotlin")
        if (compileKotlinTask != null) {
            compileKotlin.dependsOn spotlessKotlinApply
            return
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
