apply plugin: 'com.android.application'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion buildConfig.compileSdk
    defaultConfig {
        applicationId "com.github.watabee.rakutenranking"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.version.code
        versionName buildConfig.version.name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "RAKUTEN_APP_ID", "\"${propOrEnv('RAKUTEN_APP_ID')}\""
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true

            def files = project.file("proguards")
                .listFiles()
                .findAll { it.name.startsWith('proguard') }
                .toList()
                .toArray()
            files.plus(getDefaultProguardFile('proguard-android-optimize.txt'))

            proguardFiles(files)
        }
    }

    packagingOptions {
        // https://ktor.io/quickstart/migration/1.1.1.html
        // Known issues: There are duplicate META-INF files in common and jvm modules.
        exclude 'META-INF/ktor-http.kotlin_module'
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/ktor-utils.kotlin_module'
        exclude 'META-INF/kotlinx-coroutines-io.kotlin_module'
        exclude 'META-INF/ktor-client-json.kotlin_module'
        exclude 'META-INF/ktor-client-core.kotlin_module'
    }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation project(':common')
            }
        }
    }

    targets {
        fromPreset(presets.android, 'androidApp')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.kotlin.stdlib.jdk
    implementation deps.kotlin.coroutines.jdk
    implementation deps.kotlin.coroutines.android

    implementation deps.androidx.appCompat
    implementation deps.androidx.recyclerView
    implementation deps.androidx.ktx
    implementation deps.androidx.constraintLayout
    implementation deps.androidx.browser

    implementation deps.knarch.dbAndroid
    implementation deps.sqldelight.runtimeJdk
    implementation deps.sqldelight.multiplatformdriverAndroid

    implementation deps.groupie.core
    implementation deps.groupie.dataBinding

    implementation deps.glide.core
    kapt deps.glide.compiler

    implementation deps.koin.core
    implementation deps.koin.android
    implementation(deps.koin.viewModel) {
        exclude group: 'androidx.lifecycle'
    }

    implementation deps.ktor.client.okhttp
    implementation deps.timber.android
}
