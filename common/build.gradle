apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.android.library'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.squareup.sqldelight'

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        minSdkVersion buildConfig.minSdk
    }
    
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation deps.androidx.appCompat
    implementation deps.androidx.lifecycleExtension
    implementation deps.okhttp.logging
    api deps.timber
}

kotlin {
    sourceSets {
        commonMain {
            kotlin {
                srcDirs 'build/sqldelight'
            }
            dependencies {
                implementation deps.kotlin.stdlib.common
                implementation deps.kotlin.coroutines.common
                implementation deps.kotlin.serialization.common
                implementation deps.ktor.client.core
                implementation deps.ktor.client.json.core
            }
        }

        androidMain {
            dependencies {
                implementation deps.kotlin.stdlib.jdk
                implementation deps.kotlin.coroutines.jdk
                implementation deps.kotlin.coroutines.android
                implementation deps.kotlin.serialization.jdk
                implementation deps.ktor.client.okhttp
                implementation deps.ktor.client.json.jvm
                implementation deps.sqldelight.driver.android
            }
        }

        iosMain {
            dependencies {
                implementation deps.kotlin.coroutines.native
                implementation deps.kotlin.serialization.native
                implementation deps.ktor.client.ios
                implementation deps.ktor.client.json.ios
                implementation deps.sqldelight.driver.ios
            }
        }
    }

    targets {
        fromPreset(presets.android, 'android')

        iosArm64("iosArm64")
        iosX64("iosX64")

        configure([iosArm64, iosX64]) {
            binaries {
                framework()
            }
            compilations.main {
                source(sourceSets.iosMain)
                extraOpts "-Xuse-experimental=kotlin.Experimental",
                    "-Xuse-experimental=kotlin.ExperimentalMultiplatform"
            }
            compilations.each {
                it.extraOpts("-linker-options", "-lsqlite3")
            }
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

sqldelight {
    packageName = "com.github.watabee.rakutenranking.db"
}

task setupIos(type: Exec) {
    workingDir "${rootDir}/ios"
    commandLine "sh", "./scripts/setup.sh", propOrEnv("RAKUTEN_APP_ID")
}

// createIosDebugFatFramework or createIosReleaseFatFramework
project.afterEvaluate {
    final String frameworkName = "${project.name}.framework"
    final String binaryName = project.name

    ["Debug", "Release"].each { buildType ->

        final String outputDir = "${buildDir}/xcode-frameworks"
        final String frameworkOutputPath = "${outputDir}/${frameworkName}"
        final String binaryOutputPath = "${frameworkOutputPath}/${binaryName}"

        project.tasks.create("createIos${buildType}FatFramework") { task ->
            def frameworkArm64 = kotlin.targets.iosArm64.compilations.main.target.binaries.findFramework("", buildType)
            def frameworkX64 = kotlin.targets.iosX64.compilations.main.target.binaries.findFramework("", buildType)
            final String linkTaskArm64 = frameworkArm64.linkTaskName
            final String linkTaskX64 = frameworkX64.linkTaskName

            task.dependsOn linkTaskArm64
            task.dependsOn linkTaskX64

            task.doLast {
                final String outputArm64 = frameworkArm64.outputFile
                final String outputX64 = frameworkX64.outputFile

                exec { exec ->
                    new File(frameworkOutputPath).mkdirs()

                    def args = ["-create"]

                    args.addAll(["-arch", "arm64", "${outputArm64}/${binaryName}"])
                    args.addAll(["-arch", "x86_64", "${outputX64}/${binaryName}"])
                    args.addAll(["-output", "${binaryOutputPath}"])

                    exec.executable = "lipo"
                    exec.args = args
                }

                copy { copy ->
                    copy.from(outputArm64) { from ->
                        from.exclude(binaryName)
                    }
                    copy.into(frameworkOutputPath)
                }

                final String plistPath = "${frameworkOutputPath}/Info.plist"
                exec { exec ->
                    exec.executable = "/usr/libexec/PlistBuddy"
                    exec.args = ["-c", "Delete :UIRequiredDeviceCapabilities", plistPath]
                }

                exec { exec ->
                    exec.executable = "/usr/libexec/PlistBuddy"
                    exec.args = ["-c", "Add :CFBundleSupportedPlatforms:1 string iPhoneSimulator", plistPath]
                }
            }
        }
    }
}